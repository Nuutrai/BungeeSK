plugins {
    id "java"
    id 'com.gradleup.shadow' version '8.3.2'
}

group = "fr.zorg"
version = "2.1.0"

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url = "https://repo.skriptlang.org/releases" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://repo.destroystokyo.com/repository/maven-public/" }
}

dependencies {
    // bukkit/skript
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly("com.github.SkriptLang:Skript:2.9.2") {
        exclude group: "com.sk89q.worldguard", module: "worldguard-legacy"
        exclude group: "net.milkbowl.vault", module: "Vault"
    }

    // proxies
    compileOnly("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
    compileOnly("net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT")

    // libraries
    implementation("com.github.rockaport:alice:0.9.0")
    implementation("com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4")
    implementation("io.methvin:directory-watcher:0.15.1")
    implementation("org.xerial:sqlite-jdbc:3.46.1.3")
    implementation("commons-io:commons-io:2.11.0")
    compileOnly("org.jetbrains:annotations:22.0.0")
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

def oldestJava = 17
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-source", "" + oldestJava, "-target", "" + oldestJava]
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
    filesMatching("velocity-plugin.json") {
        expand props
    }
    filesMatching("bungee.json") {
        expand props
    }
    from ("lang") {
        include "**/*"
        into "lang/"
    }
}

shadowJar {
    dependencies {
        include(dependency("com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4"))
        include(dependency("com.github.rockaport:alice:0.9.0"))
        include(dependency("org.xerial:sqlite-jdbc:3.46.1.3"))
    }
}