plugins {
    id "java"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    implementation project(":BungeeSK")

    implementation group: "com.github.rockaport", name:"alice", version: "0.9.0"
    implementation group: "com.github.Carleslc.Simple-YAML", name: "Simple-Yaml", version: "1.8.4"
    implementation group: "io.methvin", name: "directory-watcher", version: "0.15.1"
    implementation group: "org.bstats", name: "bstats-velocity", version: "3.0.2"
    compileOnly group: "com.velocitypowered", name: "velocity-api", version: "3.2.0-SNAPSHOT"
    annotationProcessor group: "com.velocitypowered", name: "velocity-api", version: "3.2.0-SNAPSHOT"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

def oldestJava = 17
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-source", "" + oldestJava, "-target", "" + oldestJava]
}

tasks.named("compileJava").configure {
    dependsOn(":BungeeSK:jar")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

jar {
    archiveBaseName.set("${rootProject.name}")
    archiveVersion.set(version.toString())
    archiveClassifier.set("")
    destinationDirectory.set(file("${rootProject.buildDir}/libs/Velo"))
    from project(":BungeeSK").sourceSets.main.output
}

shadowJar {
    archiveClassifier.set("")
    dependencies {
        include(dependency("com.github.Carleslc.Simple-YAML:Simple-Yaml"))
        include(dependency("com.github.rockaport:alice"))
    }
}